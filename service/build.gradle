
/** / // Disables caching of artefacts ensuring latest snapshots are used.
configurations.all {
 // Check for updates every build
 resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}
/**/

buildscript {
  repositories {
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
  }
  dependencies {
    classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    classpath "org.grails.plugins:views-gradle:2.0.1"
    classpath "gradle.plugin.com.github.erdi.webdriver-binaries:webdriver-binaries-gradle-plugin:2.0"
    classpath "org.grails.plugins:hibernate5:${gormHibernate-".RELEASE"}"
    classpath 'org.grails.plugins:database-migration:3.1.0'
    classpath 'com.bmuschko:gradle-docker-plugin:6.6.1'
  }
}


final String version_regex = /^(((\d+)\.\d+)\.\d+)(-([a-zA-Z]+(\.\d+)?))?$/
Map<String, String> env = System.getenv();
String calculated_version = null;

if ( ( appVersion.contains('SNAPSHOT') ) && ( env.BUILD_NUMBER != null ) ) {
  println("Detected snapshot version and build number - appending");
  calculated_version = "${appVersion}.${env.BUILD_NUMBER}".toString()
}
else {
  println("Release version");
  calculated_version = "${appVersion}".toString()
}

version "${calculated_version}"
group "mod.rs"

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.grails.grails-web'
apply plugin: 'org.grails.plugins.views-json'
apply plugin: 'com.github.erdi.webdriver-binaries'
apply plugin: 'com.bmuschko.docker-remote-api'

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform
if (DefaultNativePlatform.getCurrentOperatingSystem().isWindows()) {
    grails { pathingJar = true }
}

eclipse {
  autoBuildTasks ([':processResources'])
  synchronizationTasks ([':processResources'])
  
  classpath {
    defaultOutputDir = new File(project.buildDir.canonicalPath, 'main')
    file {
      
      whenMerged { classpath ->
        
        Set<String> resources = sourceSets.main?.resources?.srcDirs?.collect { it.canonicalPath }
        
        entries.collect().each {
          
          if (it.path =='grails-app/conf' && it.output?.toLowerCase().endsWith('test')) {
            classpath.entries.remove( it )
            println "Removed ${it}"
            return
          }

          if (it.hasProperty('output') && it.output.startsWith('bin/')) {
            it.output = 'build/' + it.output.substring(4)
          }
          
          // Eclude the output of the resources added to the source sets for grails.
          // Instead we add the results of the 'processResources' tasks to the classpath later.
          if (it.hasProperty('dir') && resources.find { res -> it.dir?.canonicalPath?.startsWith(res) } ) {
            it.exported = false
            it.excludes << '**/*'
          }
        }
        def lib = new org.gradle.plugins.ide.eclipse.model.Library(fileReference(file("${tasks.processResources.destinationDir}")))
        println "Adding: ${lib}"
        entries << lib
      }
    }
  }
}

repositories {
//  mavenLocal()
  mavenCentral()
  maven { url "https://maven.k-int.com/content/repositories/public" }
  maven { url 'https://repo.grails.org/grails/core' }
  maven { url 'http://maven.indexdata.com/' }
  maven { url "https://repository.folio.org/repository/maven-folio" }
}

sourceSets {
  main {
    resources {
      srcDir 'grails-app/migrations'
      srcDir 'src/main/okapi/tenant'
    }
  }
}

dependencies {

 /**** Grails 4 *****/
  compile "org.springframework.boot:spring-boot-starter-logging"
  compile "org.springframework.boot:spring-boot-autoconfigure"
  compile "org.grails:grails-core"
  compile "org.springframework.boot:spring-boot-starter-actuator"
  compile "org.grails:grails-plugin-url-mappings"
  compile "org.grails:grails-plugin-rest"
  compile "org.grails:grails-plugin-codecs"
  compile "org.grails:grails-plugin-interceptors"
  compile "org.grails:grails-plugin-services"
  compile "org.grails:grails-plugin-datasource"
  compile "org.grails:grails-plugin-databinding"
  compile "org.grails:grails-web-boot"
  compile "org.grails:grails-logging"
  compile "org.grails.plugins:async"
  compile "org.grails.plugins:views-json"
  compile "org.grails.plugins:views-json-templates"
  compile "org.grails.plugins:events"
  compile "org.grails.plugins:hibernate5"
  compileOnly "io.micronaut:micronaut-inject-groovy"



  console ("org.grails:grails-console") {
    // The gpars transient depends on an older version of groovy-all.
    exclude group: 'org.codehaus.groovy', module: 'groovy'
  }

  profile "org.grails.profiles:rest-api"
  // runtime "com.h2database:h2"
  runtime "javax.xml.bind:jaxb-api:2.3.1"

  profile "org.grails.profiles:rest-api"




  /************** Testing ************/
  testCompile "org.grails:grails-gorm-testing-support"

  testCompile "org.mockito:mockito-core"
  testCompile "org.grails:grails-web-testing-support"
  testCompile "org.grails.plugins:geb"
  //  testCompile "io.micronaut:micronaut-http-client"
  testCompile "org.seleniumhq.selenium:selenium-remote-driver:3.14.0"
  testCompile "org.seleniumhq.selenium:selenium-api:3.14.0"
  testCompile "org.seleniumhq.selenium:selenium-support:3.14.0"
  testCompile 'org.grails:grails-datastore-rest-client:6.1.9.RELEASE'
  testRuntime "org.seleniumhq.selenium:selenium-chrome-driver:3.14.0"
  testRuntime "org.seleniumhq.selenium:selenium-firefox-driver:3.14.0"
  compile 'org.z3950.zing:cql-java:1.13'


  compile "org.springframework.boot:spring-boot-starter-undertow" // Replaces spring-boot-starter-tomcat
  compile "org.hibernate:hibernate-core:5.4.19.Final"             // Update to latest 5.4
  compile "org.hibernate:hibernate-java8:5.4.19.Final"
  runtime "com.zaxxer:HikariCP:5.0.1"
  // runtime "com.zaxxer:HikariCP:3.4.5"                             // Replaces Tomcat JDBC pool
  runtime "org.postgresql:postgresql:42.2.23"

  compile ('org.grails.plugins:database-migration:3.1.0') {       // Required by Grails Okapi
    exclude group: 'org.liquibase', module: 'liquibase-core'
  }
  compile 'org.liquibase:liquibase-core:3.9.0'

  compile 'com.opencsv:opencsv:4.6'
  compile 'commons-io:commons-io:2.7'
  
  compile 'com.k_int.grails:web-toolkit-ce:6.4.1'
  compile 'com.k_int.okapi:grails-okapi:5.0.1'
  
  
  // Data-model dependency
//  compile 'org.olf.reshare.dm.directory:dm-directory:2.0.1'
  
  compile ('org.olf.reshare.dm.directory:dm-directory:2.0.1') {       // Required by Grails Okapi
    exclude group: 'com.k_int.grails', module: 'web-toolkit-ce'
  }

  // Custom non profile deps.
  compile 'org.apache.kafka:kafka-clients:2.8.1'
  runtime "org.grails.plugins:async"
  runtime "org.grails.plugins:events"
  // compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
  compile 'io.github.http-builder-ng:http-builder-ng-apache:1.0.4'
  

  // Better test reports.
  testCompile( 'com.athaydes:spock-reports:1.6.1' ) {
    transitive = false // this avoids affecting your version of Groovy/Spock
  }

  // NCIP deps
  compile 'org.olf.rs:lib-ncip-client:0.19.5'
  compile 'com.github.jknack:handlebars:4.2.0'
  compile 'uk.co.cacoethes:groovy-handlebars-engine:0.2'
  compile 'org.apache.commons:commons-collections4:4.4'
  compile 'org.jsoup:jsoup:1.10.2'
  compile 'org.json:json:20190722'
  compile 'org.extensiblecatalog.ncip.v2:service:2.0.0'
  compile 'org.extensiblecatalog.ncip.v2:common:2.0.0'
  compile 'org.extensiblecatalog.ncip.v2:binding-ilsdiv1_0_bc:2.0.0'
  compile 'org.extensiblecatalog.ncip.v2:binding-ilsdiv1_0:2.0.0'
  compile 'org.extensiblecatalog.ncip.v2:binding-wclv1_0:2.0.0'
  compile 'org.extensiblecatalog.ncip.v2:binding-ncipv2_02:2.0.0'
  compile 'org.extensiblecatalog.ncip.v2:binding-ncipv2_01:2.0.0'
  compile 'org.extensiblecatalog.ncip.v2:binding-jar:2.0.0'

  // Domain model for directory
  compile 'org.scala-saddle:google-rfc-2445:20110304'
  // For RRule processing
  compile 'org.dmfs:lib-recur:0.11.6'
  
  // For graphviz
  compile 'guru.nidi:graphviz-java-all-j2v8:0.18.1'
  
  // For jasper reports
  compile 'net.sf.jasperreports:jasperreports:6.19.1'
  compile 'net.sf.jasperreports:jasperreports-fonts:6.19.1'
  compile 'net.sf.barcode4j:barcode4j:2.1'
  compile 'org.apache.xmlgraphics:batik-bridge:1.14'
  
  // For swagger - was trying to use t
  compile 'org.grails.plugins:swagger-grails:0.4.0'
  compile 'io.swagger.core.v3:swagger-models:2.2.2'
}

bootRun {
  ignoreExitValue true
  jvmArgs(
      '-Dspring.output.ansi.enabled=always',
      '-server',
      '-noverify',
      '-XX:TieredStopAtLevel=1',
      '-Xmx1024m')
  sourceResources sourceSets.main
  String springProfilesActive = 'spring.profiles.active'
  systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

tasks.withType(Test) {
    systemProperty "geb.env", System.getProperty('geb.env')
    systemProperty "geb.build.reportsDir", reporting.file("geb/integrationTest")
    systemProperty "webdriver.chrome.driver", System.getProperty('webdriver.chrome.driver')
    systemProperty "webdriver.gecko.driver", System.getProperty('webdriver.gecko.driver')
}

task generateDescriptors(type: Copy) {

  dependsOn buildProperties

  File grailsBuildInfoFile = buildProperties.outputs.files.files.find { it.name == 'grails.build.info' }

  final def props = [:]

  if(!(grailsBuildInfoFile)) return // No need to continue if the file is not there

  doFirst {

    // Place the loading within a do first as the info file from the buildProperties
    // task will not be present until it is executed. This owuld cause a file not found
    // during task configureation otherwise.
    Properties properties = new Properties()

    // Read properties from the file
    grailsBuildInfoFile.withInputStream {
      properties.load(it)
    }

    // Need to parse multi-dimensional keys.
    ConfigSlurper cs = new ConfigSlurper()
    props.putAll(cs.parse(properties))
  }

  from ('./src/main/okapi') {
    include '*-template.json'
  }
  
  into './build/resources/main/okapi'
  filteringCharset = 'UTF-8'
  expand(props)
  rename { String fileName ->
    fileName.replace('-template', '')
  }
}

jar {
  version "${version.replaceAll(version_regex, '$2')}"
}

buildProperties.doLast {

  // Find the right file
  File grailsBuildInfoFile = it.outputs.files.files.find { it.name == 'grails.build.info' }

  if(!grailsBuildInfoFile) return // No need to continue if the file is not there
    Properties properties = new Properties()

  // Read properties from the file
  grailsBuildInfoFile.withInputStream {
    properties.load(it)
  }

  // Load the app version
  String version = properties.getProperty('info.app.version')

  // Because order matters. The version may be missing from the properties. We should
  // re-add it if it is.
  if (version && !(version =~ version_regex)) {
    // Write the value again.
    version = project.version
    properties.setProperty('info.app.version', version)
  }
  else {
    println("No version")
  }

  def versionParts
  if (version && (versionParts = version =~ version_regex)) {
    versionParts[0].with {
      properties.setProperty('info.app.majorVersion', it[3])
      properties.setProperty('info.app.minorVersion', it[2])
      properties.setProperty('info.app.patchVersion', it[1])

      if (it[5]) {
        properties.setProperty('info.app.prVersion', it[5])
      }
    }
  }

  // Add in build number and other useful build env properties
  // Map<String, String> env = System.getenv()
  // println("Build env: ${env}")
  
  // Add new properties from various sources
  properties.setProperty('build.time', new Date().format("yyyy-MM-dd HH:mm:ss"))

  // Get a System property
  properties.setProperty('build.java.version', System.getProperty('java.version'))

  if(env.BUILD_NUMBER)
    properties.setProperty('build.number', env.BUILD_NUMBER)

  if ( env.GIT_REVISION ) {
    properties.setProperty('build.git.revision', env.GIT_REVISION)
    properties.setProperty('git.revision', env.GIT_REVISION)
  }

  if ( env.GIT_BRANCH ) {
    properties.setProperty('build.git.branch', env.GIT_BRANCH)
    properties.setProperty('git.branch', env.GIT_BRANCH)
  }

  if (env.GIT_COMMIT) {
    properties.setProperty('build.git.commit', env.GIT_COMMIT)
    properties.setProperty('git.commit', env.GIT_COMMIT)
  }


  // Write the properties back to the file
  grailsBuildInfoFile.withOutputStream {
    properties.store(it,null)
  }
}

processResources.dependsOn generateDescriptors
